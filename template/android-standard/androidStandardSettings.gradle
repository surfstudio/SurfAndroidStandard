apply from: '../android-standard/androidStandardConfig.gradle'
import groovy.json.JsonSlurper

// Скрипт для включения модулей android-standard из локального репозитория в settings.gradle

if (gradle.ext.androidStandard.debugEnabled) {
    try {
        def androidStandardDebugDir = gradle.ext.androidStandard.debugDir
        def components = new JsonSlurper().parse(new File("$androidStandardDebugDir/buildSrc/components.json"), 'utf-8')
        assert components instanceof List
        includeBuild("$androidStandardDebugDir") {
            dependencySubstitution {
                components.each { component ->
                    def disabled = component.disabled?.toBoolean() ?: false
                    if (component.libs != null && !disabled) {
                        component.libs.each { library ->
                            def artifactName = library.artifact_name
                            if (artifactName != null && !artifactName.isEmpty()) {
                                substitute module("ru.surfstudio.android:${artifactName}") with project(":${artifactName}")
                            }
                        }
                    }
                }
            }
        }
    } catch (FileNotFoundException ignored) {
        logger.error("file buildSrc/components.json  in local android standard folder  not found")
    }
}
